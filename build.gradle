plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'com.github.johnrengelman.shadow' version '7.1.+'
}

apply from: 'gradle/teacon-forge.gradle'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

// definitions at gradle/teacon-forge.gradle
teacon {
    modId = 'voteme'
    modVersion = '0.7.0'
    modLicense = 'BSD-3-Clause'
    modGitHubRepo = 'teaconmc/VoteMe'
    modAuthors = ['yuriscat (Yuri\'s Cat)', '3TUSK', 'ustc-zzzz']
    modDescription = 'Minecraft mod for in-game voting.'

    platform = 'forge-1.19.4-45.0.9'
    // parchment = '2022.05.22'

    // uncomment these lines if you need
    modName = 'Vote Me' // default to repo name
    // modGitHubBranch = 1.18-forge // for referring the license
    // modifyMemberAccess = true // for access transformer
    // useDataGeneration = true // for data generation
    publishTask = shadowJar // for shadow jar or other usages
    lazyTokens = ['minecraft_classpath': { project.configurations.shadow.asPath }] // for runtime tokens

    // use './gradlew -q printModMeta > src/main/resources/META-INF/mods.toml' to generate mod meta
}

configurations {
    compileClasspath.extendsFrom(shadow)
    runtimeClasspath.extendsFrom(shadow)
}

dependencies {
    shadow 'io.lettuce:lettuce-core:6.2.3.RELEASE'
}

shadowJar {
    archiveClassifier.set(null)
    configurations = [project.configurations.shadow]
    dependencies {
        it.exclude it.dependency('io.netty:.*')
    }
    relocate 'reactor', 'org.teacon.voteme.reactor'
    relocate 'io.lettuce', 'org.teacon.voteme.lettuce'
    relocate 'org.reactivestreams', 'org.teacon.voteme.reactivestreams'
}

jar { it.enabled false }

reobf {
    jar { it.enabled false }
}
